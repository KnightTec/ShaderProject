#include "UnityCG.cginc"
#include "FogCommon.cginc"
#pragma kernel CSMain SHADOWS NOISE LOCAL_LIGHTS JITTER MIE_SCHLICK SELF_SHADOW
#pragma kernel CSMain SELF_SHADOW ATMOSPHERE
#pragma kernel CSMain SHADOWS NOISE LOCAL_LIGHTS JITTER MIE_SCHLICK

#define PI 3.14159

struct DirLightData 
{
	float4x4 worldToShadow[4];
};
RWStructuredBuffer<DirLightData> lightData;
float3 dirLightColor;
float3 dirLightDirection;
float4 lightSplitsNear;
float4 lightSplitsFar;

#ifdef SHADOWS
SamplerState MyPointClampSampler;
Texture2D<float> cascadeShadowMap;
#endif

RWTexture3D<half4> fogVolume;
float4 cameraPosition;
float4 frustumRays[4];
float scattering;
float g;
float k;
float fogHeight;
float fogFalloff;
float4 sliceDepths[32];
float4 scatterColor;
float time;

float noiseIntensity;
float noiseSize;
float4 noiseDirection;
float4 ambientLightColor;

#ifdef LOCAL_LIGHTS
struct FogPointLight
{
	float4 position;
    float4 color;
    float range;
    float intensity;
};
RWStructuredBuffer<FogPointLight> pointLights;
#endif
uint pointLightCount;

float3 volumeResolution;
Texture2D<float> blueNoise;
float sliceProportion;

#ifdef SHADOWS
float3 getCascadeShadowCoords(float3 worldPosition) 
{
	float distToCam = distance(worldPosition, cameraPosition.xyz);
	float4 near = float4(distToCam >= lightSplitsNear); 
	float4 far = float4(distToCam < lightSplitsFar);
	float4 weights = (near * far);

	uint cascadeIndex = dot(weights, float4(0, 1, 2, 3));
	return mul(lightData[0].worldToShadow[cascadeIndex], float4(worldPosition, 1)).xyz; 
}
#endif

float3 getFrustumRay(float2 uv)
{
	float3 upperRay = lerp(frustumRays[1].xyz, frustumRays[2].xyz, uv.x);
	float3 lowerRay = lerp(frustumRays[0].xyz, frustumRays[3].xyz, uv.x);
	return lerp(lowerRay, upperRay, uv.y);
}

float3 calcTransmittance(float3 position, float3 viewDirection, float distance)
{
	// integrate optical depth along light ray
	// https://iquilezles.org/www/articles/fog/fog.htm
	float opticalDepth = EXP(-fogFalloff * (position.y + distance * viewDirection.y) + fogHeight);
	opticalDepth -= EXP(-fogFalloff * position.y + fogHeight);
	opticalDepth /= -fogFalloff * viewDirection.y;
	// return transmittance
	return EXP(-opticalDepth * scattering * scatterColor);
}

#ifdef NOISE
// noise function copied from here: https://www.ronja-tutorials.com/2018/09/15/perlin-noise.html
float rand3dTo1d(float3 value, float3 dotDir = float3(12.9898, 78.233, 37.719)){
    //make value smaller to avoid artefacts
    float3 smallValue = sin(value);
    //get scalar value from 3d vector
    float random = dot(smallValue, dotDir);
    //make value more random by making it bigger and then taking teh factional part
    random = frac(sin(random) * 143758.5453);
    return random;
}
float3 rand3dTo3d(float3 value)
{
    return float3(
        rand3dTo1d(value, float3(12.989, 78.233, 37.719)),
        rand3dTo1d(value, float3(39.346, 11.135, 83.155)),
        rand3dTo1d(value, float3(73.156, 52.235, 09.151))
    );
}
float easeIn(float interpolator)
{
	return interpolator * interpolator;
}
float easeOut(float interpolator){
	return 1 - easeIn(1 - interpolator);
}
float easeInOut(float interpolator){
	float easeInValue = easeIn(interpolator);
	float easeOutValue = easeOut(interpolator);
	return lerp(easeInValue, easeOutValue, interpolator);
}
float perlinNoise(float3 value){
	float3 fraction = frac(value);
	float interpolatorX = easeInOut(fraction.x);
	float interpolatorY = easeInOut(fraction.y);
	float interpolatorZ = easeInOut(fraction.z);
	float3 cellNoiseZ[2];
	[unroll]
	for(int z=0;z<=1;z++){
		float3 cellNoiseY[2];
		[unroll]
		for(int y=0;y<=1;y++){
			float3 cellNoiseX[2];
			[unroll]
			for(int x=0;x<=1;x++){
				float3 cell = floor(value) + float3(x, y, z);
				float3 cellDirection = rand3dTo3d(cell) * 2 - 1;
				float3 compareVector = fraction - float3(x, y, z);
				cellNoiseX[x] = dot(cellDirection, compareVector);
			}
			cellNoiseY[y] = lerp(cellNoiseX[0], cellNoiseX[1], interpolatorX);
		}
		cellNoiseZ[z] = lerp(cellNoiseY[0], cellNoiseY[1], interpolatorY);
	}
	float3 noise = lerp(cellNoiseZ[0], cellNoiseZ[1], interpolatorZ);
	return noise;
}
#endif

#ifdef MIE_HENYEY
#define PHASE(pointPosition, lightDirection) henyeyGreensteinPhaseFunction(pointPosition, lightDirection, cameraPosition, g) 
#elif MIE_SCHLICK
#define PHASE(pointPosition, lightDirection) henyeyGreensteinPhaseFunctionSchlick(pointPosition, lightDirection, cameraPosition, k) 
#else
#define PHASE(pointPosition, lightDirection) rayleighPhaseFunction(pointPosition, lightDirection, cameraPosition)
#endif

float sunLightIntensityMultiplier;

// 
[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	float3 volCoord = float3(float3(id) * volumeResolution + 0.5f * volumeResolution);
	float z = sliceDepths[id.z >> 2][id.z & 3];
#ifdef JITTER
	z *= (1 + blueNoise.Load(int3(id.xy & 63, 0)) * sliceProportion);
#endif
	float3 worldPosition = cameraPosition.xyz + z * getFrustumRay(volCoord.xy);

	float4 visibility;
#ifdef SHADOWS
	// percentage closer filtering
	float3 shadowCoord = getCascadeShadowCoords(worldPosition);
	float4 shadowDepth = cascadeShadowMap.GatherRed(MyPointClampSampler, shadowCoord.xy, int2(0, 0));
	visibility = float4(shadowDepth < shadowCoord.z);
	shadowDepth = cascadeShadowMap.GatherRed(MyPointClampSampler, shadowCoord.xy, int2(2, 0));
	visibility += float4(shadowDepth < shadowCoord.z);
	shadowDepth = cascadeShadowMap.GatherRed(MyPointClampSampler, shadowCoord.xy, int2(0, 2));
	visibility += float4(shadowDepth < shadowCoord.z);
	shadowDepth = cascadeShadowMap.GatherRed(MyPointClampSampler, shadowCoord.xy, int2(2, 2));
	visibility += float4(shadowDepth < shadowCoord.z);
	visibility.r = dot(visibility, 0.0625f);
#else
	visibility.r = 1;
#endif	

#ifdef NOISE
	float noise = saturate(perlinNoise((worldPosition - noiseDirection.xyz * time) * noiseSize));
	noise = noise * 0.7 + 0.3 *saturate(perlinNoise((worldPosition - noiseDirection.xyz * time)));
	noise = noise * noiseIntensity + (1 - noiseIntensity);

	scattering *= saturate(EXP(-(worldPosition.y * fogFalloff - fogHeight)) * noise);
#else
	scattering *= saturate(EXP(-(worldPosition.y * fogFalloff - fogHeight)));
#endif

	half4 result = half4(ambientLightColor.rgb, scattering);

#ifdef ATMOSPHERE
	float3 scatteredColor = float3(1, 1, 1);
	scatteredColor *= sunLightIntensityMultiplier;
#else
	float3 scatteredColor = dirLightColor * visibility.r;
#endif	

#ifdef SELF_SHADOW
	// transmittance from directional light to worldPosition
	float3 transmittance = calcTransmittance(worldPosition, dirLightDirection, 1000000);
	scatteredColor *= transmittance;
#endif

	float phase = PHASE(worldPosition, dirLightDirection);
	result.rgb += scatteredColor * phase;

#ifdef LOCAL_LIGHTS
	// point light lighting
	for (uint i = 0; i < pointLightCount; i++) 
	{
		FogPointLight light = pointLights[i];
		float3 lightVector = light.position.xyz - worldPosition;
		float lightDist = length(lightVector);
		if (lightDist > light.range)
		{
			continue;
		}
		float3 lightVecNorm = normalize(lightVector);
		//inverse-square light attenuation
		float attentuation = 10 / max(lightDist, 0.5f);
		attentuation *= attentuation;
		// windowing function
		float wnd = max(1 - pow(lightDist / light.range, 4), 0);
		attentuation *= wnd * wnd;

		phase = PHASE(worldPosition, lightVecNorm);
		result.rgb += light.color.rgb * attentuation * phase 
			* light.intensity;
	}
#endif
	result.rgb *= scattering * scatterColor;
	fogVolume[id] = result;
}